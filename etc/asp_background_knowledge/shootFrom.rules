fieldIsAhead(X,Y) :- field(A,B), field(X,Y), target(A,B,0), Y=B, X < A.
fieldIsAhead(X,Y) :- field(A,B), field(X,Y), target(A,B,1), X=A, Y < B.
fieldIsAhead(X,Y) :- field(A,B), field(X,Y), target(A,B,2), Y=B, X > A.
fieldIsAhead(X,Y) :- field(A,B), field(X,Y), target(A,B,3), X=A, Y > B.

wumpusAhead(X,Y) :- fieldIsAhead(X,Y), wumpusPossible(X,Y).

blockingWumpusForSelf(X,Y) :- wumpusBlocksAgent(X,Y,A), wumpusPossible(X,Y), me(A).

definitelyBlockingWumpusSelf(X,Y) :- wumpusBlocksAgent(X,Y,A), wumpus(X,Y), me(A).

wumpusUtilSelf(N) :- N = #count{1,C,D : wumpusAhead(C,D), blockingWumpusForSelf(C,D)}.
wumpusUtilDefiniteSelf(X) :- X = #count{1,C,D : wumpusAhead(C,D), definitelyBlockingWumpusSelf(C,D)}.

duplicateShotAt(N) :- N= #count{1,X,Y : fieldIsAhead(X,Y), shotAt(X,Y)}.

{target(X,Y,D) : field(X,Y), visited(X,Y), not wumpusPossible(X,Y), not trapPossible(X,Y), not wumpus(X,Y), not trap(X,Y), direction(D)} = 1.

:- not fieldIsAhead(_,_).

#maximize{N@1 : wumpusUtilDefiniteSelf(N)}.
#minimize{N@2 : duplicateShotAt(N)}.
#maximize{N@3 : wumpusUtilSelf(N)}.

targetPos(X,Y) :- target(X,Y,_).
targetHeading(D) :- target(_,_,D).
